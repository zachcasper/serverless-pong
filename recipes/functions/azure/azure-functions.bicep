@description('Information about what resource is calling this Recipe. Generated by Radius.')
param context object

var prefix string = toLower(uniqueString('${context.resource.id}'))
var name string = 'func${prefix}'

param location string

// Pull connection data and build env vars (CONNECTION_<CONN>_<PROP>), skipping provisioningState/recipe/status
var connections = context.resource.?connections ?? {}
var connectionEnvVars = flatten(
  map(
    filter(items(connections), conn => true),
    conn => map(
      filter(items(conn.value), prop => !contains([
        'provisioningState'
        'recipe'
        'status'
      ], prop.key)),
      prop => {
        name: toUpper('CONNECTION_${conn.key}_${prop.key}')
        value: string(prop.value)
      }
    )
  )
)

resource storage 'Microsoft.Storage/storageAccounts@2022-09-01' = {
  name: '${name}storage'
  location: location
  sku: {
    name: 'Standard_LRS'
  }
  kind: 'StorageV2'
  properties: {
    supportsHttpsTrafficOnly: true
    minimumTlsVersion: 'TLS1_2'
    accessTier: 'Hot'
  }
}

resource plan 'Microsoft.Web/serverfarms@2023-12-01' = {
  name: '${name}plan'
  location: location
  kind: 'functionapp'
  sku: {
    name: 'B1'
    tier: 'Basic'
    size: 'B1'
    family: 'B'
    capacity: 1
  }
  properties: {
    reserved: false
  }
}

resource functionApp 'Microsoft.Web/sites@2023-12-01' = {
  name: '${name}func'
  location: location
  kind: 'functionapp,linux'
  identity: {
    type: 'SystemAssigned'
  }
  properties: {
    serverFarmId: plan.id
    httpsOnly: true
    siteConfig: {
      linuxFxVersion: 'DOCKER|${context.resource.properties.image}'
      alwaysOn: true
      appSettings: concat([
        {
          name: 'FUNCTIONS_WORKER_RUNTIME'
          value: 'node'
        }
        {
          name: 'FUNCTIONS_EXTENSION_VERSION'
          value: '~4'
        }
        {
          name: 'AzureWebJobsStorage'
          value: 'DefaultEndpointsProtocol=https;AccountName=${storage.name};AccountKey=${storage.listKeys().keys[0].value};EndpointSuffix=${environment().suffixes.storage}'
        }
        {
          name: 'WEBSITES_ENABLE_APP_SERVICE_STORAGE'
          value: 'false'
        }
        {
          name: 'WEBSITES_PORT'
          value: '80'
        }
      ], connectionEnvVars)
    }
  }
}

output result object = {
  values: {
    url: 'https://${functionApp.properties.defaultHostName}'
  } 
}
